if Code.ensure_loaded?(:partisan_peer_service) do
  defmodule Horde.NodeListener.Partisan do
    @moduledoc """
    A Horde node listener for Partisan-based clusters.

    Uses `:partisan.monitor_nodes/1` for node monitoring and
    `:partisan_peer_service` for membership, replacing the default
    Erlang distribution assumptions in `Horde.NodeListenerBehaviour`.

    ## Usage

        {Horde.Registry,
          name: MyRegistry,
          keys: :unique,
          members: {:auto, Horde.NodeListener.Partisan}}
    """

    use Horde.NodeListenerBehaviour

    @impl GenServer
    def init(cluster) do
      :partisan.monitor_nodes(true)
      {:ok, cluster}
    end

    @impl Horde.NodeListenerBehaviour
    def make_members(cluster) do
      case :partisan_peer_service.members() do
        members when is_list(members) ->
          Enum.map(members, fn peer -> {cluster, peer.name} end)

        _ ->
          [{cluster, :partisan_config.get(:name)}]
      end
    end

    @impl Horde.NodeListenerBehaviour
    def handle_nodeup(_node, cluster), do: set_members(cluster)

    @impl Horde.NodeListenerBehaviour
    def handle_nodedown(_node, cluster), do: set_members(cluster)

    # Partisan emits 2-tuple {:nodeup, node} / {:nodedown, node} — no node_type
    @impl GenServer
    def handle_info({:nodeup, node}, cluster) do
      handle_nodeup(node, cluster)
      {:noreply, cluster}
    end

    @impl GenServer
    def handle_info({:nodedown, node}, cluster) do
      handle_nodedown(node, cluster)
      {:noreply, cluster}
    end
  end
end
